Sub 巴西帳變整理()
    Dim fd As FileDialog
    Dim vrtSelectedItem As Variant
    Dim filePath As String
    Dim wb As Workbook
    Dim ws As Worksheet
    Dim newWorkbook As Workbook
    Dim masterWs As Worksheet
    Dim targetWorkbook As Workbook
    Dim targetWs As Worksheet
    Dim lastRow As Long
    Dim masterLastRow As Long
    Dim firstRow As Long
    Dim lastRowToCopy As Long
    Dim rng As Range
    Dim isFirstFile As Boolean
    Dim fileName As String
    Dim continueMerge As Boolean
    Dim targetFilePath As String
    Dim baseFileName As String
    Dim dashPos As Long

    ' 禁用提示視窗
    Application.DisplayAlerts = False

    continueMerge = True

    Do While continueMerge
        ' 讓使用者選擇要合併的檔案
        Set fd = Application.FileDialog(msoFileDialogFilePicker)
        With fd
            .Title = "選擇要合併的 Excel 檔案"
            .Filters.Clear
            .Filters.Add "Excel Files", "*.xlsx; *.xls"
            .AllowMultiSelect = True
            If .Show = -1 Then
                ' 創建一個新的工作簿
                Set newWorkbook = Workbooks.Add
                Set masterWs = newWorkbook.Sheets(1)

                isFirstFile = True

                For Each vrtSelectedItem In .SelectedItems
                    filePath = vrtSelectedItem

                    On Error Resume Next
                    Set wb = Workbooks.Open(filePath)
                    If Err.Number <> 0 Then
                        MsgBox "無法打開檔案: " & filePath, vbExclamation
                        Err.Clear
                        GoTo NextFile
                    End If
                    On Error GoTo 0

                    Set ws = wb.Sheets(1)

                    If Not ws.Cells.Find("*", SearchOrder:=xlByRows, SearchDirection:=xlNext) Is Nothing Then
                        firstRow = ws.Cells.Find("*", SearchOrder:=xlByRows, SearchDirection:=xlNext).Row
                        lastRowToCopy = ws.Cells.Find("*", SearchOrder:=xlByRows, SearchDirection:=xlPrevious).Row

                        Set rng = ws.Rows(firstRow & ":" & lastRowToCopy)

                        If isFirstFile Then
                            rng.Copy
                            masterWs.Cells(1, 1).PasteSpecial Paste:=xlPasteAll
                            isFirstFile = False
                        Else
                            masterLastRow = masterWs.Cells(masterWs.Rows.Count, 1).End(xlUp).Row + 1
                            rng.Offset(1, 0).Resize(rng.Rows.Count - 1).Copy
                            masterWs.Cells(masterLastRow, 1).PasteSpecial Paste:=xlPasteAll
                        End If
                    End If

                    wb.Close SaveChanges:=False

NextFile:
                    DoEvents
                Next vrtSelectedItem

                With masterWs
                    .Cells.Font.Name = "Calibri"
                    .Cells.Font.Size = 11
                    .Columns("C:D").Insert Shift:=xlToRight, CopyOrigin:=xlFormatFromLeftOrAbove
                    .Columns("B:B").TextToColumns Destination:=Range("B1"), DataType:=xlFixedWidth, _
                        FieldInfo:=Array(Array(0, xlTextFormat), Array(11, xlTextFormat), Array(13, xlTextFormat)), TrailingMinusNumbers:=True
                    .Columns("C:D").Delete Shift:=xlToLeft
                    .Columns("E:E").Insert Shift:=xlToRight, CopyOrigin:=xlFormatFromLeftOrAbove
                    .Columns("D:D").TextToColumns Destination:=Range("D1"), DataType:=xlFixedWidth, _
                        FieldInfo:=Array(Array(0, xlTextFormat), Array(10, xlTextFormat)), TrailingMinusNumbers:=True
                    .Columns("E:E").Delete Shift:=xlToLeft
                    .Columns("G:G").Replace What:="-*", Replacement:="", LookAt:=xlPart, _
                        SearchOrder:=xlByRows, MatchCase:=False, SearchFormat:=False, _
                        ReplaceFormat:=False, FormulaVersion:=xlReplaceFormula2
                    masterWs.Columns("A:G").AutoFit
                End With

                Set fd = Application.FileDialog(msoFileDialogFilePicker)
                With fd
                    .Title = "選擇要儲存合併結果的站台Excel檔案"
                    .Filters.Clear
                    .Filters.Add "Excel Files", "*.xlsx; *.xls"
                    .AllowMultiSelect = False
                    If .Show = -1 Then
                        targetFilePath = .SelectedItems(1)
                    Else
                        MsgBox "沒有選擇檔案。"
                        Exit Sub
                    End If
                End With

                On Error Resume Next
                Set targetWorkbook = Workbooks.Open(targetFilePath)
                If Err.Number <> 0 Then
                    MsgBox "無法打開目標檔案: " & targetFilePath, vbExclamation
                    Err.Clear
                    Exit Sub
                End If
                On Error GoTo 0

                targetFileName = Dir(targetFilePath)
                dashPos = InStr(targetFileName, "-")
                If dashPos > 0 Then
                    baseFileName = Replace(Left(targetFileName, dashPos - 1), " ", "")
                Else
                    baseFileName = Replace(targetFileName, " ", "")
                End If

                Set targetWs = targetWorkbook.Sheets(1)

                masterWs.Range("A:G").Copy
                targetWs.Cells(1, 1).PasteSpecial Paste:=xlPasteAll
                
                targetWorkbook.RefreshAll
                
                
                ' 激活第二個工作表並切回第一列
                With targetWorkbook.Sheets(2)
                    .Activate
                    ' 切回第一列
                    .Cells(6, 1).Select
                End With
                    
                targetWorkbook.Save

                newWorkbook.Close SaveChanges:=False

                ' 問用戶是否要繼續合併其他檔案
                continueMerge = (MsgBox("是否繼續合併其他站台檔案？", vbYesNo) = vbYes)
            Else
                MsgBox "沒有選擇檔案。"
                continueMerge = False
            End If
        End With

        ' 設置 FileDialog 對象為 Nothing
        Set fd = Nothing
    Loop

    Application.DisplayAlerts = True
End Sub
